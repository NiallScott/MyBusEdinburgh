/*
 * Copyright (C) 2014 - 2023 Niall 'Rivernile' Scott
 *
 * This software is provided 'as-is', without any express or implied
 * warranty.  In no event will the authors or contributors be held liable for
 * any damages arising from the use of this software.
 *
 * The aforementioned copyright holder(s) hereby grant you a
 * non-transferrable right to use this software for any purpose (including
 * commercial applications), and to modify it and redistribute it, subject to
 * the following conditions:
 *
 *  1. This notice may not be removed or altered from any file it appears in.
 *
 *  2. Any modifications made to this software, except those defined in
 *     clause 3 of this agreement, must be released under this license, and
 *     the source code of any modifications must be made available on a
 *     publically accessible (and locateable) website, or sent to the
 *     original author of this software.
 *
 *  3. Software modifications that do not alter the functionality of the
 *     software but are simply adaptations to a specific environment are
 *     exempt from clause 2.
 */

import com.android.build.gradle.BasePlugin
import com.android.build.gradle.BaseExtension

buildscript {
    ext {
        androidCompileSdkVersion = 33
        androidMinSdkVersion = 23
        androidTargetSdkVersion = 33
        androidBuildToolsVersion = '33.0.2'

        /***********************
         * Dependency versions *
         ***********************/

        // Kotlin language
        kotlinVersion = '1.8.21'

        // Kotlin Coroutines
        kotlinCoroutinesVersion = '1.7.1'

        // Dagger/Hilt
        hiltVersion = '2.46.1'
        daggerVersion = '2.46.1'

        // AndroidX
        androidXCoreVersion = '1.10.1'
        androidXActivityVersion = '1.7.1'
        androidXFragmentVersion = '1.5.7'
        appCompatVersion = '1.6.1'
        architectureComponentsVersion = '2.6.1'
        recyclerViewVersion = '1.3.0'
        viewPager2Version = '1.1.0-beta01'
        paletteVersion = '1.0.0'
        androidXPreferenceVersion = '1.2.0'
        androidXDataStoreVersion = '1.0.0'
        constraintLayoutVersion = '2.1.4'
        roomVersion = '2.5.1'
        workManagerVersion = '2.8.1'

        // Material Design
        materialVersion = '1.9.0'

        // HTTP/networking
        okhttpVersion = '4.10.0'
        retrofitVersion = '2.9.0'
        retrofitKotlinSerializationConverterVersion = '1.0.0'

        // Serialisation
        kotlinJsonSerialisationVersion = '1.5.1'
        gsonVersion = '2.8.9'

        // Image loading
        picassoVersion = '2.71828'

        // Google Play Services
        playServicesMapsVersion = '18.1.0'
        playServicesLocationVersion = '21.0.1'

        // Google Maps utils
        googleMapsKtxVersion = '3.4.0'
        googleMapsUtilsVersion = '3.4.0'
        googleMapsUtilsKtxVersion = '3.4.0'

        // Firebase
        firebaseBomVersion = '32.0.0'

        /*******************************
         * Testing dependency versions *
         *******************************/
        junitVersion = '4.13.2'
        androidTestCoreVersion = '1.4.0'
        mockitoVersion = '5.3.1'
        mockitoKotlinVersion = '4.1.0'
        androidXCoreTesting = '2.2.0'
        hamcrestVersion = '2.2'
        espressoVersion = '3.4.0'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.5'
        classpath 'com.google.firebase:firebase-appdistribution-gradle:4.0.0'
    }
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_17.toString()
            freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
        }
    }

    plugins.withType(BasePlugin).configureEach {
        extensions.configure(BaseExtension) { extension ->
            extension.compileSdkVersion androidCompileSdkVersion
            extension.buildToolsVersion androidBuildToolsVersion

            extension.defaultConfig {
                minSdkVersion androidMinSdkVersion
                targetSdkVersion androidTargetSdkVersion
            }

            extension.compileOptions {
                sourceCompatibility = JavaVersion.VERSION_17
                targetCompatibility = JavaVersion.VERSION_17
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
